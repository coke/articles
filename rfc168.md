# RFC 168, by Johan Vromans: Built-in functions should be functions

[Proposed on 27 August 2000](https://raku.org/archive/rfc/168.html),
frozen on 20 September 2000, which was a generalization of
[RFC 26: Named operators versus functions](https://raku.org/archive/rfc/26.html)
proposed on 4 August 2000, frozen on 28 August, also by *Johan Vromans*.

Johan's proposal was to completely obliterate the difference between built-in
functions, such as `abs`, and functions defined by the user.  In Perl, `abs`
can be called both as a prefix operator (without parentheses), as well as
a function taking a single argument.

You see, Perl has this concept of built-in functions that are slightly more
equal than other functions (aka subroutines).  You can refer to a subroutine
"by reference" by prefixing `&` to its name, but that doesn't quite work like
that for built-in functions in Perl.

Nowadays, in Raku, the difference between a subroutine taking a single
positional argument, and a built-in prefix operator whose name is acceptable
as an identifier, is already minimal.  Well, actually absent.  Suppose we
want to define a prefix operator `foo` that has the same semantics as `abs`:

    sub foo(Numeric:D $value) {
        $value < 0 ?? -$value !! $value
    }

    say abs -42;  # 42
    say foo -42;  # 42

    say abs(-42); # 42
    say foo(-42); # 42

You can't really see a difference, now can you?  Well, the reason is simple:
in Raku, there **is** no real difference between the `foo` subroutine, and the
`abs` prefix operator.  They're **both** just subroutines: just look at the
[definition of the `abs` function for `Real` numbers](https://github.com/rakudo/rakudo/blob/master/src/core.c/Real.pm6#L170).

But how does that function for infix operators?  Those aren't surely
subroutines as well in Raku?  How can they be?  Something like `+` is not
a valid identifier, so you cannot define a subroutine with it?

The genius in the process from the RFC to the implementation in Raku, has
really been the idea to give a subroutine representing an infix operator, a
specially formatted name.  In the case of infix `+` operator, the subroutine
is known by the name `&infix:<+>'.  And if you look at
[its definition](https://github.com/rakudo/rakudo/blob/master/src/core.c/Numeric.pm6#L210),
you'll see that it is actually quite simple: the left hand side of the infix
operator becomes the first positional argument, and the right hand side the
second positional argument.  So something like:

    say 42 + 666;

is really just syntactic sugar for:

    say infix:<+>(42, 666);

Does this apply to **all** built-in operators in Raku?  Well, *almost*.
Some operators, such as `||`, 'or', `&&` and `and` are *short-circuiting*.
This means that the value on the right hand side, might *not* be evaluated
if the left hand side has a certain value.

A simple example using the `say` function (which always returns `True`):

    say "foo" or say "bar"; # foo

Because the infix `or` operator sees that its left hand side is already
`True`, it will not bother to evaluate the right hand side, and thus will
**not** print "bar".  There is currently no way in Raku to mimic the
short-circuiting behaviour in "ordinary" subroutines.  But this will change
when macro's will finally also become first-class citizens in Raku land.
Which is expected to be happening in the coming year as part of *Jonathan
Worthington*s work on the RakuAST grant.

The RFC also mentions:

    In particular, it is desired that every built-in
    - can be overridden by a user defined subroutine;
    - can have a reference taken;
    - has a useful prototype.

So, let's check that those points:

## can be overridden by a used defined subroutine

Ok, so infix operators have a special name.  So what happens if I declare a
subroutine with that name?  Well, let's try:

    sub infix:<+>(\a, \b) { a + b }
    say 42 + 666;

Hmmm... that doesn't show anything, that just hangs!  Well, yeah, because we
basically have a case of a subroutine here calling itself without ever
returning!

**This eats about 1GB of memory per second, so don't do that too long unless you have a lot of memory available!**

The easiest fix would be to *not* use the infix `+` operator in our version:

    sub infix:<+>(\a, \b) { sum a, b }
    say 42 + 666;  # 708

But what if we want to refer to original `&infix:<+>` logic?  It's just a
subroutine after all!  But where does that subroutine live?  Well, in the core
of course!  And for looking up things in the core, you use the `CORE::`
`PseudoStash`:

    sub infix:<+>(\a, \b) {
        say "plussing";
        CORE::<&infix:<+>>(a, b)
    }
    say 42 + 666; # plussing\n708

You look in the `CORE::` pseudostash for the full name of the infix operator:
`CORE::<&infix:<+>>` will then give you the subroutine object of the core's
infix `+` operator, and you can call that as a subroutine with two parameters.

So that part of the RFC has been implemented!

## can have a reference taken

For the infix `+` operator, that would be `&infix:<+>`, as basically is shown
in the example above.  You could actually store that in a variable, and use
that later in an expression:

    my $foo = &infix:<+>;
    say $foo(42,666);  # 708

So in that sense, the RFC has also been implemented.

## has a useful prototype

Perl's prototypes basically morphed into Raku's
[Signature](https://docs.raku.org/type/Signature)s.  But that's at least one
blog post all by itself.  So for now, we just say "prototypes" of Perl in
2000 turned into signatures in Raku.  And since you can ask for a subroutine's
signature:

    sub foo(\a, \b) { }
    say &foo.signature;  # (\a, \b)

You can also do that for the infix `+` operator:

    say &infix:<+>.signature;  # ($?, $?, *%)

Hmmm... that looks different?  Well, yes, it does a bit, but what is mainly
different is that both positional parameters are optional.  And that **any**
named parameters will also be accepted.  As to why that is, that's really
the topic of a yet another blog post about meta-operators.  Which we'll also
leave for another time.

# Conclusion

RFC's 168 and 26 have been implemented completely, although maybe not in the
way the original RFC's envisioned.  In a way that nowadays just feels very
natural.  Which allows us to build further, on the shoulders of giants!
